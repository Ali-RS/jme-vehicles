// Gradle settings and tasks common to all jme-vehicles subprojects

apply plugin: 'java'

ext {
    // module coordinates of dependencies:
    garrettCoordinates = 'com.github.stephengold:Garrett:0.4.0'
    heartCoordinates = 'com.github.stephengold:Heart:7.4.1'
    jmeTtfCoordinates = 'com.github.stephengold:jme-ttf:2.2.4'
    minieCoordinates = 'com.github.stephengold:Minie:4.8.0'
    skyControlCoordinates = 'com.github.stephengold:SkyControl:1.0.2'

    // current versions of libraries:
    jme3Version = '3.5.1-stable'
    maVehiclesVersion = '0.7.1-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true // to provide detailed deprecation warnings
    options.encoding = 'UTF-8'
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_1_10)) {
        options.release = 8
    }
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform;
Boolean isMacOS = DefaultNativePlatform.currentOperatingSystem.isMacOsX();

tasks.withType(JavaExec) { // Java runtime options:
    if (isMacOS) {
        jvmArgs '-XstartOnFirstThread'
    }
    enableAssertions true
    //jvmArgs '-verbose:gc'
    jvmArgs '-Xms512m', '-Xmx512m', '-XX:MaxDirectMemorySize=1200m'
    jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

repositories {
    //mavenLocal() // to find local SNAPSHOTs of libraries
    mavenCentral()
    maven { url 'https://jcenter.bintray.com' } // for Lemur and SimMath
}

// custom tasks for creating source/javadoc JARs:

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    dependsOn 'javadoc'
    description 'Creates a JAR of javadoc.'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    description 'Creates a JAR of sourcecode.'
    from sourceSets.main.allJava
}
