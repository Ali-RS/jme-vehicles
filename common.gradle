// Gradle settings and tasks common to all jme-vehicles subprojects

apply plugin: 'java'

ext { // current versions of various libraries:
    garrettVersion = '0.2.0'
    heartVersion = '7.1.0'
    jmeTtfVersion = '2.2.4'
    jme3Version = '3.4.0-stable'
    maVehiclesVersion = '0.7.1-SNAPSHOT'
    minieVersion = '4.5.0-test1'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) { // Java compile-time options:
        options.compilerArgs << '-Xdiags:verbose'
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
        options.encoding = 'UTF-8'
    }
    tasks.withType(JavaExec) { // Java runtime options:
        enableAssertions true
        //jvmArgs '-verbose:gc'
        jvmArgs '-Xms512m', '-Xmx512m', '-XX:MaxDirectMemorySize=1g'
        jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
    }
}

repositories {
    //mavenLocal()
    mavenCentral()
    maven { url 'https://jcenter.bintray.com' } // for Lemur and SimMath
}

// custom tasks for creating source/javadoc JARs

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    dependsOn 'javadoc'
    description 'Creates a JAR of javadoc.'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    description 'Creates a JAR of sourcecode.'
    from sourceSets.main.allJava
}
