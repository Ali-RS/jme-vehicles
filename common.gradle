// Gradle settings and tasks common to all subprojects

apply plugin: 'java'

group = 'com.github.stephengold'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) { // Java compile-time options:
        options.compilerArgs << '-Xdiags:verbose'
        //options.compilerArgs << '-Xlint:unchecked'
        //options.deprecation = true
        options.encoding = 'UTF-8'
    }
    tasks.withType(JavaExec) { // Java runtime options:
        enableAssertions true
        //jvmArgs '-verbose:gc'
        jvmArgs '-Xms512m', '-Xmx512m', '-XX:MaxDirectMemorySize=1g'
        jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
    }
}

ext {
    // current versions of various libraries
    garrettVersion = '0.1.2'
    heartVersion = '6.4.2'
    jmeTtfVersion = '2.2.2'
    jmonkeyengineVersion = '3.3.2-stable'
    maVehiclesVersion = '0.5.1-SNAPSHOT'
    minieVersion = '4.0.0+big3'
    pbrTerrainVersion = '1.0.1'
}

repositories {
    //mavenLocal()
    mavenCentral()
    jcenter()
}

// custom tasks for creating source/javadoc JARs
task sourcesJar(type: Jar) {
    classifier = 'sources'
    dependsOn 'classes'
    description 'Create a JAR of source files.'
    from sourceSets.main.allJava
}
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    dependsOn 'javadoc'
    description 'Create a JAR of javadoc.'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives jar, sourcesJar, javadocJar
}