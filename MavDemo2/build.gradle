// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'application'
}

mainClassName = 'com.jayfella.jme.vehicle.niftydemo.MavDemo2'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

dependencies {
    runtimeOnly 'org.jmonkeyengine:jme3-desktop:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-lwjgl3:' + jme3Version

    // MavDemo2 doesn't use jme3-plugins
    //  -- it is included solely to avoid warnings from AssetConfig.
    runtimeOnly 'org.jmonkeyengine:jme3-plugins:' + jme3Version

    // non-JME dependencies:
    runtimeOnly 'com.github.nifty-gui:nifty-style-black:1.4.3'
    implementation 'com.github.stephengold:Acorus:0.9.15'
    implementation 'com.github.stephengold:jme3-utilities-nifty:0.9.29'
    implementation garrettCoordinates
    implementation 'com.github.stephengold:JmePower:0.4.4'
    implementation minieCoordinates
    implementation skyControlCoordinates

    implementation project(':MavCommon')
    implementation project(':MavLibrary')
    //implementation 'com.github.stephengold:MaVehicles:' + maVehiclesVersion
}

tasks.register('debugForceDialog', JavaExec) {
    args = ['--forceDialog']
    classpath sourceSets.main.runtimeClasspath
    debug true
    mainClass = mainClassName
}

tasks.register('runForceDialog', JavaExec) {
    args = ['--forceDialog']
    classpath sourceSets.main.runtimeClasspath
    mainClass = mainClassName
}

// cleanup tasks:

clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs')
clean.dependsOn('cleanScreenShots')

tasks.register('cleanDLLs', Delete) { // extracted Windows native libraries
    delete fileTree(dir: '.', include: '*.dll')
}
tasks.register('cleanDyLibs', Delete) { // extracted macOS native libraries
    delete fileTree(dir: '.', include: '*.dylib')
}
tasks.register('cleanLogs', Delete) { // JVM crash logs
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
tasks.register('cleanSOs', Delete) { // extracted Linux and Android native libraries
    delete fileTree(dir: '.', include: '*.so')
}
tasks.register('cleanScreenShots', Delete) {
    delete fileTree(dir: '.', include: 'screen_shot*.png')
}
