// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

plugins {
    id 'application'
}

mainClassName = 'com.jayfella.jme.vehicle.lemurdemo.MavDemo1'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

dependencies {
    runtimeOnly 'org.jmonkeyengine:jme3-desktop:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jme3Version
    runtimeOnly 'org.jmonkeyengine:jme3-lwjgl3:' + jme3Version

    // MavDemo1 doesn't use jme3-plugins
    //  -- it is included solely to avoid warnings from AssetConfig.
    runtimeOnly 'org.jmonkeyengine:jme3-plugins:' + jme3Version

    // non-JME dependencies:
    implementation garrettCoordinates
    implementation 'com.github.stephengold:LemurPower:0.4.4'
    implementation minieCoordinates
    implementation 'com.simsilica:lemur-props:1.2.0'
    runtimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.17.2'
    runtimeOnly 'org.codehaus.groovy:groovy-jsr223:3.0.10'

    implementation project(':MavCommon')
    implementation project(':MavLibrary')
    //implementation 'com.github.stephengold:MaVehicles:' + maVehiclesVersion
}

tasks.register('debugForceDialog', JavaExec) {
    args = ['--forceDialog']
    classpath sourceSets.main.runtimeClasspath
    debug true
    mainClass = mainClassName
}

tasks.register('runForceDialog', JavaExec) {
    args = ['--forceDialog']
    classpath sourceSets.main.runtimeClasspath
    mainClass = mainClassName
}

// cleanup tasks:

clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs')
clean.dependsOn('cleanScreenShots')

tasks.register('cleanDLLs', Delete) { // extracted Windows native libraries
    delete fileTree(dir: '.', include: '*.dll')
}
tasks.register('cleanDyLibs', Delete) { // extracted macOS native libraries
    delete fileTree(dir: '.', include: '*.dylib')
}
tasks.register('cleanLogs', Delete) { // JVM crash logs
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
tasks.register('cleanSOs', Delete) { // extracted Linux and Android native libraries
    delete fileTree(dir: '.', include: '*.so')
}
tasks.register('cleanScreenShots', Delete) {
    delete fileTree(dir: '.', include: 'screen_shot*.png')
}
