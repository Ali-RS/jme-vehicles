// Note: "common.gradle" in the root project contains additional initialization
//   for this project. This initialization is applied in the "build.gradle"
//   of the root project.

apply plugin: 'application'

description = 'a tech demo for jMonkeyEngine vehicles'

mainClassName = 'com.jayfella.jme.vehicle.lemurdemo.MavDemo1'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

dependencies {
    runtimeOnly 'org.jmonkeyengine:jme3-desktop:' + jmonkeyengineVersion
    runtimeOnly 'org.jmonkeyengine:jme3-jogg:' + jmonkeyengineVersion
    runtimeOnly 'org.jmonkeyengine:jme3-lwjgl3:' + jmonkeyengineVersion

    // MavDemo1 doesn't use jme3-plugins
    //  -- it is added solely to avoid warnings from AssetConfig
    runtimeOnly 'org.jmonkeyengine:jme3-plugins:' + jmonkeyengineVersion

    // non-JME dependencies:
    implementation 'com.github.stephengold:Garrett:' + garrettVersion
    implementation 'com.github.stephengold:LemurPower:0.4.2'
    implementation 'com.github.stephengold:Minie:' + minieVersion
    implementation 'com.simsilica:lemur-props:1.1.1'
    runtimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1'
    runtimeOnly 'org.codehaus.groovy:groovy-jsr223:3.0.8'

    implementation project(':MavCommon')
    implementation project(':MavLibrary')
    //implementation 'com.github.stephengold:MaVehicles:' + maVehiclesVersion
}

task debugForceDialog(type: JavaExec) {
    args = ['--forceDialog']
    classpath sourceSets.main.runtimeClasspath
    debug true
    mainClass = mainClassName
}

task runForceDialog(type: JavaExec) {
    args = ['--forceDialog']
    classpath sourceSets.main.runtimeClasspath
    mainClass = mainClassName
}

// cleanup tasks
clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs')
clean.dependsOn('cleanScreenShots')
task cleanDLLs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanDyLibs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dylib')
}
task cleanLogs(type: Delete) {
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
task cleanSOs(type: Delete) {
    delete fileTree(dir: '.', include: '*.so')
}
task cleanScreenShots(type: Delete) {
    delete fileTree(dir: '.', include: 'screen_shot*.png')
}
